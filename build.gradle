plugins {
    id 'fabric-loom' version '1.7-SNAPSHOT'
    id 'checkstyle'
    id 'eclipse'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}


version = project.mod_version
group = project.maven_group
base.archivesName = project.mod_id

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
    withSourcesJar()
}

loom {
    splitEnvironmentSourceSets()
    
    mods {
        terramap {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
    
    runs {
        client {
            client()
            configName = "Fabric Client"
            ideConfigGenerated = true
            runDir = "run"
        }
        server {
            server()
            configName = "Fabric Server"
            ideConfigGenerated = true
            runDir = "run"
        }
    }
}

repositories {
    maven {
        name = "Fabric"
        url = "https://maven.fabricmc.net/"
    }
    maven {
        name = "Smyler Snapshots"
        url = "https://maven.smyler.net/snapshots/"
    }
	maven {
   		name = "JitPack"
        url = "https://jitpack.io/"
    }
    maven { //used for leveldb
        name = "DaPorkchop_"
        url = "https://maven.daporkchop.net/"
    }
    maven { //used for leveldb
        name = "OpenCollab Snapshots"
        url = "https://repo.opencollab.dev/snapshot/"
    }
    mavenCentral()
}

dependencies {
    // Fabric dependencies
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // Image processing
    implementation group: "com.twelvemonkeys.imageio", name: "imageio-webp", version: project.twelvemonkeys_version

    // TerraMinusMinus for coordinate projection (replaces Terraplusplus)
    implementation "net.buildtheearth.terraminusminus:terraminusminus-core:${project.terraminusminus_version}"

    runtimeOnly group: "org.apache.commons", name: "commons-imaging", version: "1.0-alpha2"
    testImplementation group: "org.junit.jupiter", name: "junit-jupiter", version: "5.8.1"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
               "minecraft_version": project.minecraft_version,
               "loader_version": project.loader_version,
               "fabric_version": project.fabric_version
    }
}

shadowJar {
    configurations = [project.configurations.shadow]
    archiveClassifier.set('')
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
    finalizedBy('shadowJar')
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

test {
    useJUnitPlatform()
}
